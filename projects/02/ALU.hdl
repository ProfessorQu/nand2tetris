// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Set x according to zx and nx
    Mux16(a=x, sel=zx, out=x1);
    Not16(in=x1, out=negateX);
    Mux16(a=x1, b=negateX, sel=nx, out=x2);

    // Set y according to zy and ny
    Mux16(a=y, sel=zy, out=y1);
    Not16(in=y1, out=negateY);
    Mux16(a=y1, b=negateY, sel=ny, out=y2);

    // Compute function
    Add16(a=x2, b=y2, out=xyAdded);
    And16(a=x2, b=y2, out=xyAnd);
    Mux16(a=xyAnd, b=xyAdded, sel=f, out=out1);

    // Not out
    Not16(in=out1, out=notOut);
    Mux16(a=out1, b=notOut, sel=no, out=out);

    Mux16(a=out1, b=notOut, sel=no, out[15]=msb);
    Mux16(a=out1, b=notOut, sel=no, out[0..7]=out1Half, out[8..15]=out2Half);

    // Out zero
    Or8Way(in=out1Half, out=orTemp00);
    Or8Way(in=out2Half, out=orTemp01);
    Or(a=orTemp00, b=orTemp01, out=orTemp11);
    Not(in=orTemp11, out=zr);

    // Out negative
    Or(a=msb, out=ng);
}